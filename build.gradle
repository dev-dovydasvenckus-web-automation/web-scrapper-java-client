plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}


sourceCompatibility = '1.11'
targetCompatibility = '1.11'

ext {
    isCIServer = System.getenv().isCIServer
}

java {
    withJavadocJar()
    withSourcesJar()
}
repositories {
    mavenLocal()
    mavenCentral()
}

test {
  useJUnitPlatform()
}

dependencies {
    implementation 'org.glassfish.jersey.core:jersey-client:3.0.3'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:3.0.3'
    implementation 'org.glassfish.jersey.media:jersey-media-json-jackson:3.0.3'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.mock-server:mockserver-junit-jupiter:5.11.1'
    testImplementation 'org.assertj:assertj-core:3.21.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

}


signing {
    sign publishing.publications
    if (isCIServer) {
        println("This is CI")
        def signingKey = System.env.signingKey
        def signingPassword = System.env.signingPassword
        useInMemoryPgpKeys(signingKey, signingPassword)
    }
}

group = 'dev.dovydasvenckus'
version = '0.0.2'

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = 'Web scrapper client'
                description = 'Client for https://github.com/dovydasvenckus/web-scrapper'
                url = 'https://github.com/dovydasvenckus/web-scrapper-java-client'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'https://github.com/dovydasvenckus'
                        name = 'Dovydas Venckus'
                        url = 'https://dovydasvenckus.dev'
                    }
                }
                scm {
                    connection = 'git@github.com:dovydasvenckus/web-scrapper-java-client.git'
                    developerConnection = 'git@github.com:dovydasvenckus/web-scrapper-java-client.git'
                    url = 'https://github.com/dovydasvenckus/web-scrapper-java-client'
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri('https://s01.oss.sonatype.org/service/local/')
            snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            username = "$System.env.ossrhUsername"
            password = "$System.env.ossrhPassword"
        }
    }
}
